#define FIND(x) as(@eqfind(as(x, number)), Id)
.functor eqfind(number):number

.decl N(n : number)
.input N(IO=file, filename="N.in")

.type Id = Add {x : Id, y : Id}
         | Var {n : number}

.decl eql(x : Id, y : Id) eqrel
.decl add(x : Id, y : Id, c : Id) btree_delete
.decl var(x : number, c : Id) btree_delete

.decl add_prep(x: Id, y : Id, c : Id)
var(1, $Var(1)).
var(2, $Var(2)).
add_prep($Var(2), $Var(1), $Add($Var(2), $Var(1))).
add_prep($Var(x + 1), id, $Add($Var(x + 1), id)), 
 var(x + 1, $Var(x + 1))
 :-
 add_prep($Var(x), _, id),
 x <= n, N(n).

add(x, y, c) :- add_prep(x, y, c).

// populating the eql relation
eql(x, x) :- var(_, x).
eql(x, x) :- add(_, _, x).

// congruence
eql(c1, c2) :- add(x, y, c1), add(x, y, c2), c1 != c2.
eql(c1, c2) :- var(x, c1), var(x, c2), c1 != c2.

// commutativity
eql(xy, yx),
add(x, y, xy)
 :-
 add(y, x, yx),
 xy = $Add(x, y).

// associativity
eql(xy_z, x_yz), 
 add(x, yz, x_yz), 
 add(y, z, yz) 
 :- 
 add(x, y, xy), add(xy, z, xy_z),
 yz = $Add(y, z), x_yz = $Add(x, yz).

// canonize enodes
add(x1, y1, c1) :- add(x, y, c), leader(x, x1), leader(y, y1), leader(c, c1).
var(x, c1) :- var(x, c), leader(c, c1).

add(x1, y1, c1) <= add(x2, y2, c2) :-
    leader(x1, x2), leader(y1, y2), leader(c1, c2).
    .plan 0:(1, 3, 4, 5, 2), 1:(1, 3, 4, 5, 2), 2:(1, 3, 4, 5, 2), 3:(1, 3, 4, 5, 2), 4:(1, 3, 4, 5, 2), 5:(1, 3, 4, 5, 2)
var(x, c1) <= var(x, c2) :-
    leader(c1, c2).

.decl ids(a : Id)
ids(x) :- add(_, _, x).
ids(x) :- var(_, x).

.decl leader(a : Id, b : Id)

leader(a, FIND(a)) :- ids(a), eql(a, a).
// leader(a, b) :- eql(a, a), a != FIND(a), leader(FIND(a), b).
leader(x, y1) <= leader(x, y2) :-
    eql(y1, y2),
    y2 = FIND(y1).

// leader(a, as(b, Id)) :- eql(a, a), a = FIND(a), b = min b1 : { eql(a, b), b1 = as(b, number) }.
// leader(a, b) :- eql(a, a), a != FIND(a), leader(FIND(a), b).
// leader(x, y1) <= leader(x, y2) :-
//     eql(y1, y2),
//     y1 >= y2.

.decl class(a : Id)
class(b) :- leader(_, b).

.decl root(i : number, a : Id)
// (((1 + 2) + 3) + 4)
root(1, lx1234) :-
    var(1, x1), leader(x1, lx1), 
    var(2, x2), leader(x2, lx2), 
    var(3, x3), leader(x3, lx3), 
    var(4, x4), leader(x4, lx4), 
    add(lx1, lx2, x12), leader(x12, lx12),
    add(lx12, lx3, x123), leader(x123, lx123),
    add(lx123, lx4, x1234), leader(x1234, lx1234).

// (((4 + 3) + 2) + 1)
root(2, lx4321) :-
    var(1, x1), leader(x1, lx1), 
    var(2, x2), leader(x2, lx2), 
    var(3, x3), leader(x3, lx3), 
    var(4, x4), leader(x4, lx4), 
    add(lx4, lx3, x43), leader(x43, lx43),
    add(lx43, lx2, x432), leader(x432, lx432),
    add(lx432, lx1, x4321), leader(x4321, lx4321).

.output leader
.output class
.output root